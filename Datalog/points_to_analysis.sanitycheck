_visitedType(t:Type) input
_visitedMethod(m:Method) input
_primitiveArrayType(t:Type) input
_objectArrayType(array:Type, element:Type) input
_ignoredStaticInvoke(m:Method, type:Type, selector:Selector) input

_isArrayType(type:Type)
_isArrayType(t) :- _primitiveArrayType(t).
_isArrayType(t) :- _objectArrayType(t, _).

_usedObj(m:Method, o:Object)
_usedObj(m, o) :- new(m, _, o).
_usedObj(m, o) :- schedule(m, _, _, o, _).	
_usedObj(m, o) :- constant(m, _, o).

_WarningUnresolvedType(t:Type) outputtuples
_WarningUnresolvedType(t) :- \
	executionContext(_, _, _, _, m), \
	variableType(m, _, t), \
	!_isArrayType(t), \
	!_visitedType(t).
	
_WarningUnresolvedType(elementType) :- \
	executionContext(_, _, _, _, m), \
	variableType(m, _, t), \
	_objectArrayType(t, elementType), \
	!_visitedType(elementType).

_WarningUnresolvedType(t) :- \
	executionContext(_, _, _, _, m), \
	_usedObj(m, o), \
	objectType(o, t), \
	!_isArrayType(t), \
	!_visitedType(t).
	
_WarningUnresolvedType(elementType) :- \
	executionContext(_, _, _, _, m), \
	_usedObj(m, o), \
	objectType(o, t), \
	_objectArrayType(t, elementType), \
	!_visitedType(elementType).

##
##
_varHasValue(nowCtxt:Object, now:Object, thisCtxt:Object, this:Object, m:Method, var:Variable)
_varHasValue(nowCtxt, now, thisCtxt, this, m, var) :- variablePT(nowCtxt, now, thisCtxt, this, m, var, _, _).

_WarningCallsWithoutReceiver(method:Method, type:Type, var:Variable, selector:Selector) outputtuples
_WarningCallsWithoutReceiver(m, t, recVar, selector) :- \
	executionContext(nowCtxt, now, thisCtxt, this, m), \
	virtualInvoke(m, invokeBC, selector), \
	actual(m, invokeBC, 0, recVar), \
	variableType(m, recVar, t), \
	! _varHasValue(nowCtxt, now, thisCtxt, this, m, var).
	
#0 is static call, 1 is dynamic call		
_UCKind 2

_WarningUnresolvedCalls(kind:_UCKind, type:Type, selector:Selector) outputtuples
_WarningUnresolvedCalls(0, t, s) :- \
	executionContext(_, _, _, _, m), \
	_ignoredStaticInvoke(m, t, s).
_WarningUnresolvedCalls(1, t, s) :- \
	executionContext(nowCtxt, now, thisCtxt, this, m), \
	virtualInvoke(m, invokeBC, s), \
	actual(m, invokeBC, 0, recVar), \
	variablePT(nowCtxt, now, thisCtxt, this, m, var, _, calledThis), \
	objectType(calledThis, t), \
	!_isArrayType(t), \
	!_visitedType(t).

_WarningUnknownMethod(method:Method) outputtuples
_WarningUnknownMethod(m) :- \
	executionContext(_, _, _, _, m), \
	!_visitedMethod(m).
	
